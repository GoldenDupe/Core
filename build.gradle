plugins {
    // Main java library
    id 'java'

    // Shadow jar to relocate jars
    // Shadow gradle is broken currently so using googler's one works for now.
    //id 'com.github.johnrengelman.shadow' version '8.1.1'
    id("io.github.goooler.shadow") version "8.1.3"


    id 'io.freefair.lombok' version '5.1.0'

}

group = 'xyz.goldendupe'
version = '1.0'

repositories {
    mavenLocal() // Soon to be removed
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' } // Vault

}

dependencies {
    implementation 'bet.astral:Messenger:1.5.1' // Messenger
    implementation 'bet.astral:GUIMan:1.1'

    implementation 'com.samjakob:SpiGUI:1.3.1' // SpiGUI - Easier GUIs

    //compileOnly 'org.reflections:reflections:0.10.2' // Reflections - Use classgraph for faster reflections
    implementation 'io.github.classgraph:classgraph:4.8.165'

    // Easier commands using this framework
    implementation 'org.incendo:cloud-core:2.0.0-beta.2'
    implementation 'org.incendo:cloud-paper:2.0.0-beta.2'
    implementation 'org.incendo:cloud-minecraft-extras:2.0.0-beta.2'

    implementation 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'


    compileOnly "com.github.MilkBowl:VaultAPI:1.7"  // Vault
    compileOnly 'net.luckperms:api:5.4' // Luckperms

    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT" // Paper MC


}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['*.yml']) {
        expand([version: version])
    }
}

jar {
    enabled = false
    dependsOn(shadowJar)
}

shadowJar {
    archiveClassifier = ""

    relocate 'bet.astral.messenger', 'xyz.goldendupe.libs.bet.astral.messenger'

    relocate 'io.github.classgraph', "xyz.goldendupe.libs.io.github.classgraph"
    relocate 'com.samjakob', 'xyz.goldendupe.libs.com.samjakob.spigui'
    relocate 'org.incendo.cloud', 'xyz.goldendupe.libs.org.incendo.cloud'
}