plugins {
    // Main java library
    id 'java'

    // Shadow jar to relocate jars
    // Shadow gradle is broken currently so using googler's one works for now.
    //id 'com.github.johnrengelman.shadow' version '8.1.1'
    id("io.github.goooler.shadow") version "8.1.3"


    id 'io.freefair.lombok' version '5.1.0'

    id("io.papermc.paperweight.userdev") version "1.5.10"

    id("xyz.jpenilla.run-paper") version "2.2.2"

}

group = 'xyz.goldendupe'
version = '1.0'

repositories {
    mavenLocal() // Soon to be removed
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' } // Vault

    maven {
        url "https://libraries.minecraft.net"
    }
}


dependencies {
    implementation 'bet.astral:Messenger:1.5.1' // Messenger

    // have it as compile only as it needs to run as it's own plugin
    compileOnly 'bet.astral:Fluffy:1.0' // Fluffy (Combat System)
    // Factions/Clans
    compileOnly 'bet.astral:Unity:1.0'

    //compileOnly 'org.reflections:reflections:0.10.2' // Reflections - Use classgraph for faster reflections
    implementation 'io.github.classgraph:classgraph:4.8.165'


    compileOnly 'com.mojang:brigadier:1.0.18'
    // Easier commands using this framework
    var cloudVersion = "2.0.0-beta.4";
    implementation 'org.incendo:cloud-core:'+cloudVersion
    implementation 'org.incendo:cloud-paper:'+cloudVersion
    implementation 'org.incendo:cloud-brigadier:'+cloudVersion
    implementation 'org.incendo:cloud-minecraft-extras:'+cloudVersion

    implementation 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    implementation 'org.apache.commons:commons-math3:3.6.1'

    compileOnly "com.github.MilkBowl:VaultAPI:1.7"  // Vault
    compileOnly 'net.luckperms:api:5.4' // Luckperms

    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT" // Paper MC
    // Other Dependencies
    paperweight.paperDevBundle("1.20.4-R0.1-SNAPSHOT")
}

// Some shit is importing 1.13 bukkit api and not 1.20.1 api
configurations.compileOnly {
    exclude group: 'org.spigotmc', module: 'spigot'
    exclude group: 'org.spigotmc', module: 'spigot-api'
    exclude group: 'org.bukkit', module: 'bukkit'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['*.yml']) {
        expand([version: version])
    }
}

jar {
    enabled = false
    dependsOn(shadowJar)
}

shadowJar {
    archiveClassifier = null

    relocate 'bet.astral.messenger', 'xyz.goldendupe.libs.bet.astral.messenger'

    relocate 'io.github.classgraph', "xyz.goldendupe.libs.io.github.classgraph"
    relocate 'org.incendo.cloud', 'xyz.goldendupe.libs.org.incendo.cloud'
    relocate 'org.apache.commons', 'xyz.goldendupe.libs.org.apache.commons'
}

tasks {
    runServer {
        minecraftVersion("1.20.4")
    }
}