plugins {
    // Main java library
    id 'java'

    // Shadow jar to relocate jars
    // Shadow gradle is broken currently so using googler's one works for now.
    //id 'com.github.johnrengelman.shadow' version '8.1.1'
    id("io.github.goooler.shadow") version "8.1.8"
    id 'io.freefair.lombok' version '8.6'
    id("io.papermc.paperweight.userdev") version "1.7.1"
    id("xyz.jpenilla.run-paper") version "2.3.0"
}

group = 'xyz.goldendupe'
version = '1.0'

repositories {
    mavenLocal() // Soon to be removed
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' } // Vault

    maven {
        url "https://libraries.minecraft.net"
    }


    // Cloud snapshots
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
        name "sonatype-snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}


dependencies {
    implementation 'bet.astral:messenger:2.0.0' // Messenger
    implementation 'bet.astral:messenger-cloud:2.0.0' // Messenger
    implementation 'bet.astral:messenger-paper:2.0.0' // Messenger
    implementation 'bet.astral:tuples:1.0.0'
    implementation 'bet.astral:cloudplusplus:1.2.5'
    implementation 'bet.astral:cloudplusplus-paper:1.2.5'
    implementation 'bet.astral:guiman:1.0.0'

    //compileOnly 'org.reflections:reflections:0.10.2' // Reflections - Use classgraph for faster reflections
    compileOnly 'io.github.classgraph:classgraph:4.8.165'

    // Cloud and brigadier for commands
    compileOnly 'com.mojang:brigadier:1.0.18'
    var cloudMineccraft = "2.0.0-beta.8";
    implementation 'org.incendo:cloud-paper:' + cloudMineccraft
    implementation 'org.incendo:cloud-brigadier:' + cloudMineccraft
    implementation 'org.incendo:cloud-minecraft-extras:' + cloudMineccraft

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    compileOnly 'org.apache.commons:commons-math3:3.6.1'

    compileOnly "com.github.MilkBowl:VaultAPI:1.7"  // Vault
    compileOnly 'net.luckperms:api:5.4' // Luckperms

    paperweight.paperDevBundle("1.21-R0.1-SNAPSHOT")
}

// Some shit is importing 1.13 bukkit api and not 1.20.1 api
configurations.compileOnly {
    exclude group: 'org.spigotmc', module: 'spigot'
    exclude group: 'org.spigotmc', module: 'spigot-api'
    exclude group: 'org.bukkit', module: 'bukkit'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['*.yml']) {
        expand([version: version])
    }
}

jar {
    enabled = false
    dependsOn(shadowJar)
}

shadowJar {
    archiveClassifier = null
    relocate 'bet.astral.messenger', 'xyz.goldendupe.libs.messenger'
    relocate 'bet.astral.cloudplusplus', 'xyz.goldendupe.libs.cloudplusplus'
    relocate 'bet.astral.tuples', 'xyz.goldendupe.libs.tuples'
    relocate 'org.incendo.cloud', 'xyz.goldendupe.libs.org.incendo.cloud'
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}